import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.6'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.flywaydb.flyway' version '10.1.0'
	id 'org.openapi.generator' version '7.2.0'
	id 'io.freefair.lombok' version '8.4'
}

group = 'com.fdobrotv'
version = '1.0.0-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.flywaydb:flyway-core'

	compileOnly 'org.projectlombok:lombok'

	// https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-ui
	implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
	// https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	// https://mvnrepository.com/artifact/javax.validation/validation-api
	implementation 'javax.validation:validation-api:2.0.1.Final'
	// https://mvnrepository.com/artifact/org.openapitools/jackson-databind-nullable
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

	implementation 'io.hypersistence:hypersistence-utils-hibernate-62:3.6.1'
	implementation 'org.hibernate:hibernate-spatial:6.1.7.Final'

	implementation 'com.google.guava:guava:33.0.0-jre'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// https://mvnrepository.com/artifact/org.assertj/assertj-core
	testImplementation 'org.assertj:assertj-core:3.24.2'
	testImplementation 'io.jenetics:jpx:3.1.0'

}

tasks.named('test') {
	useJUnitPlatform()
}

task buildSpringBackEndAPI(type: GenerateTask) {
	generatorName.set("spring")
	inputSpec.set("$rootDir/specs/test-phone-booking-v1.0.yaml")
	outputDir.set("$rootDir")
	apiPackage.set("com.fdobrotv.testphonebooking.api")
	invokerPackage.set("com.fdobrotv.testphonebooking.invoker")
	modelPackage.set("com.fdobrotv.testphonebooking.dto")
	configOptions.set([
			dateLibrary: "java8"
	])
}

task buildTypescriptFetchSDK(type: GenerateTask) {
	generatorName = "typescript-fetch"

	inputSpec = "$rootDir/specs/test-phone-booking-v1.0.yaml".toString()
	outputDir = "$rootDir/testphonebooking-front/generated"
	apiPackage = "com.fdobrotv.testphonebooking.api"
	invokerPackage = "com.fdobrotv.testphonebooking.invoker"
	modelPackage = "com.fdobrotv.testphonebooking.dto"
}